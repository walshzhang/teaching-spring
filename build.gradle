buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.2")
    }
}

configure(rootProject) {
    apply plugin: 'org.asciidoctor.convert'

    asciidoctor {
        sourceDir project.file('src/reference')
        options doctype: 'book', ruby: 'erubis'
    }
}

allprojects {
    apply plugin: 'java'

    group 'walsh.teaching'
    version '1.0'

    ext {
        junitVersion = '4.12'
        guavaVersion = '18.0'
        mockitoVersion = '1.10.19'
        springVersion = '4.1.7.RELEASE'
    }
}

subprojects {
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        compile("com.google.guava:guava:${guavaVersion}")
        testCompile("junit:junit:${junitVersion}")
        testCompile("org.mockito:mockito-core:${mockitoVersion}")
    }
}

configure(subprojects.findAll { it.name.startsWith('spring') }) {
    dependencies {
        compile("org.springframework:spring-context:${springVersion}")
        testCompile("org.springframework:spring-test:${springVersion}")
    }
}

project(':spring-jdbc') {
    dependencies {
        testCompile('com.github.springtestdbunit:spring-test-dbunit:1.2.1')
        compile("org.springframework:spring-jdbc:${springVersion}")
        runtime('com.h2database:h2:1.4.187')
        runtime('org.dbunit:dbunit:2.5.1')
    }
}